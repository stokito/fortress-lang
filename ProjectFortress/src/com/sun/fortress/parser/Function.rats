/*******************************************************************************
    Copyright 2007 Sun Microsystems, Inc.,
    4150 Network Circle, Santa Clara, California 95054, U.S.A.
    All rights reserved.

    U.S. Government Rights - Commercial software.
    Government users are subject to the Sun Microsystems, Inc. standard
    license agreement and applicable provisions of the FAR and its supplements.

    Use is subject to license terms.

    This distribution may include materials developed by third parties.

    Sun, Sun Microsystems, the Sun logo and Java are trademarks or registered
    trademarks of Sun Microsystems, Inc. in the U.S. and other countries.
 ******************************************************************************/

/*
 * Definition of Fortress function declarations.
 */
module Function(Param, Header, Type, NoNewlineExpr, Identifier, Keyword, Symbol,
                Spacing);

import Param;
import Header;
import Type;
import NoNewlineExpr;
import Identifier;
import Keyword;
import Symbol;
import Spacing;

/* FnDecl ::=
   FnMods? FnHeaderFront FnHeaderClause (w = w NoNewlineExpr)?
 | FnSig
 */
Decl FnDecl =
     a1:FnMods? a2:FnHeaderFront a3:FnHeaderClause a4:(w void:equals w NoNewlineExpr)?
     { if (a1 == null) a1 = FortressUtil.emptyModifiers();
       Span span = createSpan(yyStart,yyCount);
       if (a4 == null) yyValue = FortressUtil.mkAbsFnDecl(span, a1, a2, a3);
       else            yyValue = FortressUtil.mkFnDecl(span, a1, a2, a3, a4);
     }
   / FnSig ;

/* FnSig ::= Name w : w ArrowType */
AbsFnDecl FnSig =
     a1:Name w colon w a2:ArrowType
     { List<Modifier> mods = FortressUtil.emptyModifiers();
       List<Param>  params = FortressUtil.emptyParams();
       for (TypeRef i : a2.getDomain()) {
           params.add(NodeFactory.makeParam(i.getSpan(), mods,
                                            new Id(i.getSpan(), "_"), i));
       }
       yyValue = FortressUtil.mkAbsFnDecl(createSpan(yyStart,yyCount), mods,
                                          (FnName)a1, params, a2.getRange());
     };

/* AbsFnDecl ::=
     AbsFnMods? FnHeaderFront FnHeaderClause
   | FnSig
 */
AbsFnDecl AbsFnDecl =
     a1:AbsFnMods? a2:FnHeaderFront a3:FnHeaderClause
     { if (a1 == null) a1 = FortressUtil.emptyModifiers();
       yyValue = FortressUtil.mkAbsFnDecl(createSpan(yyStart,yyCount),a1,a2,a3);
     }
   / FnSig ;

/* FnHeaderFront ::=
     Id (w StaticParams)? w ValParam
   | OpHeaderFront
 */
FnHeaderFront FnHeaderFront =
     a1:Id a2:(w StaticParams)? w a3:ValParam
     { if (a2 == null) a2 = FortressUtil.emptyStaticParams();
       yyValue = new FnHeaderFront(new Fun(createSpan(yyStart,yyCount),a1),a2,a3);
     }
   / OpHeaderFront ;
